*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied 
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C2M0045170D Spice Library 
****	Version 1.0 Date: 04-06-2017
****	Version 2.0 Date: 02-05-2018
****	Version 3.0 Date: 05-25-2018
***********************************************************
****	Revision record
****	Version 1	Initial Release
****	Version 2	Compactible to both Ltspice and OrCad Pspice
****	Version 3	Improved accuracy of the model
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C2M0045170D d g s Tj Tc 

xgmos	d3 d1 g1 s1 Tj Tc gmos_C2M0045170D

RS		s	sa	0.975m
Ls 		sa	s1	2.603n
*R_Ls 		sa	s1      5

R_g 		g1	g2	1.3
RG		g	ga	27.2m
Lg 		ga	g3	8.47244n
*R_Lg		ga	g3	1000

Rd		d	da	64.94u
Ld 		da	d3      3.848n
R_Ld		da	d3	5

G1		d2	d1      Value {V(d1,s1)}
R_A		d2	d1	1E6
vgate_s		g3	g2	0
vdrain_s	d3	d2	0
Gheat		0	Tj	value = {abs((V(d,s)*I(Vdrain_s)))+abs((V(g1,g2)*I(Vgate_s)))}
xCGD		d2	g1	cgdmos_C2M0045170D 
CGS		g1	s1	3665.3p
D1		s1	d2	bodydiode_C2M0045170D


R0 N1 Tj 3.55m
R1 N2 N1 10.7m
R2 N3 N2 15.7m
R3 N4 N3 36.6m
R4 N5 N4 19.2m
R5 N6 N5 8.23m
R6 N7 N6 19.9m
R7 N8 N7 20.8m
R8 N9 N8 32.3m
R9 N10 N9 33.8m
R10 N11 N10 22.1m
R11 N12 N11 10.1m
R12 N13 N12 4.11m
R13 Tc N13 2.79m
C0 Tj 0 4.89m
C1 N1 0 18.3m
C2 N2 0 46.3m
C3 N3 0 19.1m
C4 N4 0 19.8m
C5 N5 0 94.3m
C6 N6 0 0.149
C7 N7 0 0.121
C8 N8 0 0.132
C9 N9 0 0.121
C10 N10 0 0.174
C11 N11 0 0.432
C12 N12 0 2.04
C13 N13 0 28.9

.ends C2M0045170D

*****************************************

.subckt gmos_C2M0045170D d3 d1 g1 s1 Tj Tc

e1		NET1	0	Value {-0.1224*v(gk,s1)+3.5048}
R_a		NET1	0 	1E6

e3		NET3	0	Value {16.8u*(V(Tj)**2)-7.696m*V(Tj)+2.47}
R_C		NET3	0 	1E6

e2		NET2	0	Value {8.1m*V(gk,s1)+30m}
R_B		NET2	0 	1E6

e4		NET4	0	Value {8.71u*(V(Tj)**2)-4.4847m*V(Tj)+0.5067}
R_d		NET4	0 	1E6

e10		NET10   0	Value {0.001955*(V(gk,s1)**2)-0.05266*V(gk,s1)+0.36714+V(NET11)}
R_K		NET10	0	1E6

e11		NET11   0	Value {-2.403u*(V(Tj)**2)-651.43u*V(Tj)+0.151788}
R_L		NET11	0	1E6


e5		NET5	0	value {
+				if(V(gk,s1)>18.2,
+					-236.11n*(V(Tj)**2)-465.08u*v(Tj)+129.77m   
+					,
+					0
+					)
+					}
R_e		NET5	0 	1E6

e6		NET6	0	value {
+				if(V(gk,s1)>16 & V(gk,s1)<=18.2,
+					-890.69n*(V(Tj)**2)-444.13u*v(Tj)+0.14566
+					,
+					0
+					)
+					}
R_f		NET6	0 	1E6

e7		NET7	0	value {
+				if(V(gk,s1)>14 & V(gk,s1)<=16,
+					-1.52227u*(V(Tj)**2)-453.6u*v(Tj)+0.1663
+					,
+					0
+					)
+					}
R_g		NET7	0 	1E6

e8		NET8	0	value {
+				if(V(gk,s1)>12 & V(gk,s1)<=14,
+					-4.68u*(V(Tj)**2)-100.97u*v(Tj)+0.19745  
+					,
+					0
+					)
+					}
R_h		NET8	0 	1E6

e9		NET9	0	value {
+				if(V(gk,s1)>10 & V(gk,s1)<=12,
+					-6.345u*(V(Tj)**2)+166.36u*v(Tj)+0.2248    
+					,
+					0
+					)
+					}
R_j		NET9	0 	1E6

e12		NET12	0	value {
+				if(V(gk,s1)>v(NET3) & V(gk,s1)<=10,
+					-7.709u*(V(Tj)**2)+668.99u*v(Tj)+0.2341  
+					,
+					0
+					)
+					}
R_m		NET12	0 	1E6
.param p8  = 0.0011
.param p11 = -10
.param p12 = 25



R100 gk s1	1E6
E100 gk s1 	value {limit(V(g1,s1),p11,p12)}


********************************
G1 d1 s1 value {
+	if(V(s1,d3)<0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		-((0.035)*(v(gk,s1)-V(NET3)))*(-(1+p8*v(s1,d3))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)
+		-((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(s1,d3)))))**2))
+		,
+		-(((v(NET5)+v(NET6)+v(NET7)+v(NET8)+v(NET9)+v(NET12))*(v(gk,s1)-V(NET3)))+v(NET4))*(1+p8*v(s1,d3))*
+		(((log(1+exp(v(gk,s1)-V(NET3))))**2)-((log(1+exp(v(gk,s1)-V(NET3)
+		-(V(NET2)*v(s1,d3)*((1+exp(-v(NET10)*v(s1,d3)))**v(NET1))))))**2))
+		)
+			)
+			}
G2 d1 s1 value {
+	if(V(d3,s1)<0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		((0.035)*(v(gk,s1)-V(NET3)))*(-(1+p8*v(d3,s1))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)
+		-((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(d3,s1)))))**2))
+		,
+		(((v(NET5)+v(NET6)+v(NET7)+v(NET8)+v(NET9)+v(NET12))*(v(gk,s1)-V(NET3))))*(1+p8*v(d3,s1))*
+		(((log(1+exp(v(gk,s1)-V(NET3))))**2)-((log(1+exp(v(gk,s1)-V(NET3)
+		-(V(NET2)*v(d3,s1)*((1+exp(-v(NET10)*v(d3,s1)))**v(NET1))))))**2))
+		)
+			)
+			}



.ends gmos_C2M0045170D

****************************************
.model bodydiode_C2M0045170D d(is=68.89n cjo=2970p bv=2200 m=0.45 EG=3.60827 vj=1.45 n=6.5 
+	rs=0.022 trs1=1u  trs2=-20.5003u Tnom=25
+	tt=15.5n ibv=500u Xti=0.13 level=1)
****************************************

.subckt cgdmos_C2M0045170D d2 g1
.param k1=1045p
.param k2=0.508
.param ka=30
.param kb=0.7
.param kc=6.5277
G11 g1 d1 value {
+		k1*(
+		(1+v(d2,g1)*(1+ka*(1+TANH(kb*V(d2,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d2))
+			}
R_CGD d1 d2 1e-4
.ends cgdmos_C2M0045170D










