*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied 
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET CPM3-0900-0010A die Spice Library 
****	Version 1.0 Date: 11-24-2017
****	Version 2.1 Date: 01-11-2018
****	Version 3.0 Date: 05-24-2019
***********************************************************
****	Revision record
****	Version 1	Initial Release
****	Version 2	Updated the model format to Pspice compatible
****	Version 3	Improved transfer curve
*********************************************************************************
****Parasitics excluded
****Tj = Junction Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt CPM3-0900-0010A d g s Tj 

xgmos	d3 d1 g1 s1 Tj gmos_CPM3-0900-0010A

R_Ls 		s	s1      1u

Rg 		g1	g2	1.6
R_Lg		g	g2	1u

R_Ld		d	d3	1u

Ed	Id	0	value {I(Vdrain_s)}
Rdd	Id	0	1E6


vdrain_s	d3	d1	0

Gheat		0	Tj	value = {abs((V(d,s1)*v(Id)))+abs((V(g1,g2)*V(g1,g2)/1.6))}
xCGD		d3	g1	cgdmos_CPM3-0900-0010A 
CGS		g1	s1	4448p
D1		s1	d1	bodydiode_CPM3-0900-0010A
xCDS		d3	s1	cds_CPM3-0900-0010A

.ends CPM3-0900-0010A

*****************************************

.subckt gmos_CPM3-0900-0010A d3 d1 g1 s1 Tj

e1		NET1	0	value {-17.754u*(V(Tj)**2)-1.7826m*V(Tj)+955.66m}    
R_k		NET1	0 	1E6

e2		NET2	0	Value {0.035*v(gk,s1)+0.175}
R_B		NET2	0 	1E6

e3		NET3	0	Value {0.00001*(V(Tj)**2)-0.0073*V(Tj)+2.5308}
R_C		NET3	0 	1E6

e4		NET4	0	Value {-266.67u*V(Tj)+46.67m}
R_d		NET4	0 	1E6

*e5		NET5	0	value {1.35}
e5		NET5	0	value {(27.48n*V(Tj)**2-8.063u*V(Tj)+913.4u)*(V(gk,s1)**3)+
+					(-1.151u*V(Tj)**2+368.7u*V(Tj)-28.37m)*(V(gk,s1)**2)+
+					(17.18u*V(Tj)**2-5.615m*V(Tj)+170.3m)*v(gk,s1)+(-93.66u*V(Tj)**2+27.76m*V(Tj)+1.204)
+					}
R_e		NET5	0 	1E6


.param p8  = 0.0011
.param p11 = -8
.param p12 = 19


R100 gk s1	1E6
E100 gk s1 	value {limit(V(g1,s1),p11,p12)}

********************************
G1 d1 s1 value {
+	if(V(s1,d3)<=0,
+		0
+		,
+		if (V(gk,s1)<V(NET3) ,
+		-((0.000000005))*(-(1+p8*v(s1,d3))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(s1,d3)))))**2))
+		,
+		-(((v(NET5))*(v(gk,s1)-V(NET3)))+v(NET4)+v(NET1))*(1+p8*v(s1,d3))*
+		(((log(1+exp(v(gk,s1)-V(NET3))))**2)-((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(s1,d3)))))**2))
+		)
+			)
+			}

G2 d1 s1 value {
+	if(V(d3,s1)<=0,
+		0
+		,
+		if (V(gk,s1)<V(NET3) ,
+		0.000000005*(-(1+p8*v(d3,s1))*0.0080)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(s1,d3)))))**2))
+		,
+		(((v(NET5))*(v(gk,s1)-V(NET3)))+V(NET4))*(1+p8*v(d3,s1))*
+		(((log(1+exp(v(gk,s1)-V(NET3))))**2)-((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(d3,s1)))))**2))
+		)
+			)
+			}

.ends gmos_CPM3-0900-0010A

****************************************

.subckt cgdmos_CPM3-0900-0010A d3 g1
.param k1=1900p	
.param k2=0.49		
.param ka=45	
.param kb=0.3	
.param kc=6.5277	
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0,600))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1e-4
.ends cgdmos_CPM3-0900-0010A


.subckt cds_CPM3-0900-0010A d3 s1

.param Cjo = 7600p
.param Vj  = 5.5208436
.param M   = 0.68006

G12 d1 s1 value {
+	if(V(d3,s1)>0,
+		(Cjo/(1+(limit(v(d3,s1),0,530)/Vj)**M))*ddt(v(d3,s1))
+		,
+		0
+			)
+			}

R_CDS d1 d3 1E-4

.ends cds_CPM3-0900-0010A
****************************************
.model bodydiode_CPM3-0900-0010A d(is=55.89n bv=1160
+	EG=5.42827 n=7.55 rs=0.0065 trs1=-3m  trs2=25u Tnom=25
+	tt=2n ibv=500u Xti=0.13 level=1)





