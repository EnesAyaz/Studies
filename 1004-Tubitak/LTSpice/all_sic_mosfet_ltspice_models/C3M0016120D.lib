*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Wolfspeed SiC MOSFET C3M0016120D Spice Library
****	Revision 1.0 Date: 11-21-2019
****
***********************************************************
****	Revision record
****	Revision 1	Initial Release - Datasheet 08-2019
****
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C3M0016120D d g s Tj Tc 
.param Rgint = 2.6

xgmos	d3 d1 g1 s1 Tj s gmos_C3M0016120D

RS		s	sa	1.356m
Ls 		sa	s1	6n
*R_Ls		sa	s1	10

R_g 		g1	g2	{Rgint}
RG		g	ga	12.38m
Lg 		ga	g2	9.906n
*R_Lg		ga	g2	10

Rd		d	da	64.94u
Ld 		da	d3      3.848n
R_Ld		da	d3	12

vdrain_s	d3	d1	0
Gheat		0	Tj	value {abs((V(d1,s1)*I(Vdrain_s)))+abs((V(g1,g2)**2/Rgint))}
xCGD		d3	g1	cgdmos_C3M0016120D
CGS		g1	s1	6072p
xCds		d3	s1	cds_C3M0016120D
D1		s1	d1	bodydiode_C3M0016120D

*R_ds	g1	gb	1



R0 N1 Tj 41.15m
R1 N2 N1 90.58m
R2 N3 N2 47.43m
R3 Tc N3 91.1m

C0 Tj 0 2.803m
C1 N1 0 16.49m
C2 N2 0 42.52m
C3 N3 0 129.6m
.ends C3M0016120D

*****************************************

.subckt gmos_C3M0016120D d3 d1 g1 s1 Tj s

e1		NET1	0	Value {((-255.2u*V(Tj)**2+37.84m*V(Tj)-1.62)*(V(gk,s1)**3)+
+					(6.641m*V(Tj)**2-918.8m*V(Tj)+63.82)*(V(gk,s1)**2)+
+					(-53.83m*V(Tj)**2+6.374*V(Tj)-674.9)*v(gk,s1)+
+					(112.1m*V(Tj)**2-9.35*V(Tj)+3313))/1000
+					}
R_a		NET1	0 	1E6

e2		NET2	0	Value {15.35m*V(gk,s1)+371.85m}
R_B		NET2	0 	1E6

e3		NET3	0	Value {7u*(V(Tj)**2)-4.9m*V(Tj)+2.6921}
R_C		NET3	0 	1E6

e4		NET4	0	Value {limit(1/(2.4647m*V(Tj)**2+1.0626*V(Tj)+71.8943),0.002,0.055)}
R_d		NET4	0 	1E6

*e_5	NET5	0	Value {0.12}
e5		NET5	0	value {limit(((-8.8626u*V(Tj)**2+949.06u*V(Tj)+66.453m)*(V(gk,s1)**4)+
+					(300.23u*V(Tj)**2-30.28m*V(Tj)-2.2707)*(V(gk,s1)**3)+
+					(-3.0829m*V(Tj)**2+302.99m*V(Tj)+24.562)*(V(gk,s1)**2)+
+					(8.5352m*V(Tj)**2-1.282*V(Tj)-112.69)*v(gk,s1)+
+					(3.4075m*V(Tj)**2+4.6265*V(Tj)+561.01)),63,1200)/
+					(-875.373u*(V(Tj)**2)+0.44841*V(Tj)+909.3369)
+			}
*e5		NET5	0	value {limit(((1.737545u*V(Tj)**2-227.6679u*V(Tj)-11.94098m)*(V(gk,s1)**5)+
*+					(-98.95912u*V(Tj)**2+12.47161m*V(Tj)+0.673499)*(V(gk,s1)**4)+
*+					(2.102054m*V(Tj)**2-0.2524409*V(Tj)-13.88152)*(V(gk,s1)**3)+
*+					(-20.40363m*V(Tj)**2+2.324816*V(Tj)+125.9153)*(V(gk,s1)**2)+
*+					(88.69208m*V(Tj)**2-9.925428*V(Tj)-493.0806)*v(gk,s1)+
*+					(-0.1410682*V(Tj)**2+18.64979*V(Tj)+995.5209)),3,1200)/1020
*+					}
R_e		NET5	0 	1E6

e10		NET10   0	Value {Limit(((-354.3u*V(Tj)**2+76.423m*V(Tj)-1.064)*(V(gk,s1)**3)+
+					(12.427m*V(Tj)**2-2.659*V(Tj)+35.5723)*(V(gk,s1)**2)+
+					(-140.3m*V(Tj)**2+29.81*V(Tj)-398.2)*v(gk,s1)+
+					(505.64m*V(Tj)**2-107.4*V(Tj)+1671.3))/1000,0,1)
+				      }
R_K		NET10	0	1E6

.param p10  = 0.0011
.param p11 = -8
.param p12 = 19
.param p13 = 15

e_p8	P8	0	Value {Limit(((-4.804u*V(Tj)**2+1.312m*V(Tj)-0.105)*(V(gk,s1)**3)+
+					(39.27u*V(Tj)**2-24.21m*V(Tj)+3.489)*(V(gk,s1)**2)+
+					(343.8u*V(Tj)**2+70.85m*V(Tj)-20.6)*v(gk,s1)+
+					(-1.195m*V(Tj)**2-10.84m*V(Tj)+41.48))/
+					(1000),0.00001,1)
+					}
R_R	P8	0 	1E6

R100 gk s1	1E6
E100 gk s1	value {limit(V(g1,s1),p11,p12)}


********************************
G1 d1 s1 value {
+	if(V(s1,d3)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		-((0.035)*(v(gk,s1)-V(NET3)))*(-(1+p10*v(s1,d3))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(s1,d3)))))**2))
+		,
+		-((v(NET5)+v(NET4))*(v(gk,s1)-V(NET3)))*(1+v(P8)*v(s1,d3))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(s1,d3)*((1+exp(-v(NET10)*v(s1,d3)))**v(NET1))))))**2))
+		)
+			)
+			}

G2 d1 s1 value {
+	if(V(d3,s1)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		((0.035)*(v(gk,s1)-V(NET3)))*(-(1+p10*v(d3,s1))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(d3,s1)))))**2))
+		,
+		((v(NET5)*(v(gk,s1)-V(NET3))))*(1+v(P8)*v(d3,s1))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(d3,s1)*((1+exp(-v(NET10)*v(d3,s1)))**v(NET1))))))**2))
+		)
+			)
+			}

.ends gmos_C3M0016120D

****************************************
.subckt cgdmos_C3M0016120D d3 g1

.param k1=2665p
.param k2=0.5
.param ka=70
.param kb=0.3
.param kc=4.03
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0,520))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1E-3
.ends cgdmos_C3M0016120D


.subckt cds_C3M0016120D d3 s1

.param Cjo = 5179p
.param Vj  = 4.4365
.param M   = 0.643

G12 d1 s1 value {
+	if(V(d3,s1)>0,
+		(Cjo/(1+(limit(v(d3,s1),0,583)/Vj)**M))*ddt(v(d1,s1));
+		,
+		0
+			)
+			}

R_CDS d1 d3 0.0115

.ends cds_C3M0016120D
****************************************
.model bodydiode_C3M0016120D d(is=400n bv=1590 EG=5.5 n=8.5
+	rs=0.0115  trs1=0.0005  trs2=-0.000011 Tnom=25
+	tt=25n ibv=500u Xti=0.13 level=1)






