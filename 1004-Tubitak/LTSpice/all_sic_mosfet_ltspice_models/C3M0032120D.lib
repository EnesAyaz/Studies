*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Wolfspeed SiC MOSFET C3M0032120D Spice Library
****	Revision 1.0 Date: 11-14-2019
****
***********************************************************
****	Revision record
****	Revision 1	Initial Release - Datasheet 08-2019
****
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C3M0032120D d g s Tj Tc 
.param Rgint = 1.75

xgmos	d3 d1 g1 s1 Tj s gmos_C3M0032120D

RS		s	sa	1.896m
Ls 		sa	s1	5n
R_Ls		sa	s1	10

R_g 		g1	g2	{Rgint}
RG		g	ga	12.56m
Lg 		ga	g2	10.1933n
R_Lg		ga	g2	10

Rd		d	da	64.94u
Ld 		da	d3      3.848n
R_Ld		da	d3	8

vdrain_s	d3	d1	0
Gheat		0	Tj	value {abs((V(d1,s1)*I(Vdrain_s)))+abs((V(g1,g2)**2/Rgint))}
xCGD		d3	g1	cgdmos_C3M0032120D
CGS		g1	s1	3350p
xCds		d3	s1	cds_C3M0032120D
D1		s1	d1	bodydiode_C3M0032120D

R_ds	d1	s1	1.5T


R0 N1 Tj 51.889m
R1 N2 N1 139.008m
R2 N3 N2 105.831m
R3 Tc N3 148.683m

C0 Tj 0 1.612m
C1 N1 0 7.933m
C2 N2 0 24.943m
C3 N3 0 118.248m

.ends C3M0032120D

*****************************************

.subckt gmos_C3M0032120D d3 d1 g1 s1 Tj s

e1		NET1	0	Value {((-197.6u*V(Tj)**2+16.59m*V(Tj)+0.8547)*(V(gk,s1)**3)+
+					(5.279m*V(Tj)**2-0.3973*V(Tj)-5.998)*(V(gk,s1)**2)+
+					(-46.92m*V(Tj)**2+3.104*V(Tj)-133.4)*v(gk,s1)+
+					(0.1178*V(Tj)**2-6.694*V(Tj)+2522))/1000
+					}
R_a		NET1	0 	1E6

e2		NET2	0	Value {15.36m*V(gk,s1)+0.37166}
R_B		NET2	0 	1E6

e3		NET3	0	Value {8u*(V(Tj)**2)-4m*V(Tj)+2.5845}
R_C		NET3	0 	1E6

e4		NET4	0	Value {limit((410.256n*(V(Tj)**2)-55.385u*V(Tj)+2.128m),0,0.055)}
R_d		NET4	0 	1E6

e5		NET5   0	Value {
+				if (V(Tj)>=25,
+				Limit(((-153.4u*V(Tj)-36.745m)*V(ge)**5+
+					(5.9867m*V(Tj)+2.3523)*V(ge)**4+
+					(-51.113m*V(Tj)-55.573)*V(ge)**3+
+					(-0.361*V(Tj)+582.416)*V(ge)**2+
+					(4.5025*V(Tj)-2590.051)*v(ge)+
+					(2.78m*V(Tj)+4614.316))/10000,0.0001,5)
+				,
+				Limit(((1.321u*V(Tj)**2-434.2u*V(Tj)-30.55m)*V(ge)**5+
+					(-99.41u*V(Tj)**2+23.89m*V(Tj)+1.967)*V(ge)**4+
+					(2.736m*V(Tj)**2-0.4919*V(Tj)-46.264)*V(ge)**3+
+					(-33.22m*V(Tj)**2+4.628*V(Tj)+478.464)*V(ge)**2+
+					(0.1728*V(Tj)**2-20.359*V(Tj)-2076.533)*v(ge)+
+					(-0.3026*V(Tj)**2+42.328*V(Tj)+3745.312))/10000,0.0001,5)
+					)
+					}
R_e		NET5	0 	1E6

e10		NET10   0	Value {Limit(((-78.35u*V(Tj)**2+39.21m*V(Tj)-2.181)*(V(gk,s1)**3)+
+					(3.011m*V(Tj)**2-1.412*V(Tj)+75.91)*(V(gk,s1)**2)+
+					(-37.55m*V(Tj)**2+16.49*V(Tj)-862.5)*v(gk,s1)+
+					(0.1513*V(Tj)**2-62.52*V(Tj)+3329))/1000,0.01,1)
+				      }
R_K		NET10	0	1E6

.param p10  = 0.0011
.param p11 = -8
.param p12 = 19

e_p8	P8	0	Value {Limit(((-45.23u*V(Tj)**2+8.432m*V(Tj)-0.2257)*(V(gk,s1)**3)+
+					(1.324m*V(Tj)**2-0.2578*V(Tj)+7.858)*(V(gk,s1)**2)+
+					(-12.64m*V(Tj)**2+2.507*V(Tj)-70.70)*v(gk,s1)+
+					(39.38m*V(Tj)**2-7.854*V(Tj)+218.1))/1000,0.015,0.5)
+					}
R_R	P8	0 	1E6

R101 ge 0	1E6
E101 ge 0	value {limit(V(g1,s),p11,p12)}

R100 gk s1	1E6
E100 gk s1	value {limit(V(g1,s1),p11,p12)}

********************************
G1 d1 s1 value {
+	if(V(s1,d3)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		-((0.035)*(v(gk,s1)-V(NET3)))*(-(1+p10*v(s1,d3))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(s1,d3)))))**2))
+		,
+		-((v(NET5)+v(NET4))*(v(gk,s1)-V(NET3)))*(1+v(P8)*v(s1,d3))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(s1,d3)*((1+exp(-v(NET10)*v(s1,d3)))**v(NET1))))))**2))
+		)
+			)
+			}

G2 d1 s1 value {
+	if(V(d3,s1)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		((0.035)*(v(gk,s1)-V(NET3)))*(-(1+p10*v(d3,s1))*0.008)*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(0.854*v(d3,s1)))))**2))
+		,
+		((v(NET5)*(v(gk,s1)-V(NET3))))*(1+v(P8)*v(d3,s1))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(d3,s1)*((1+exp(-v(NET10)*v(d3,s1)))**v(NET1))))))**2))
+		)
+			)
+			}

.ends gmos_C3M0032120D

****************************************
.subckt cgdmos_C3M0032120D d3 g1

.param k1=1263p
.param k2=0.5
.param ka=40
.param kb=0.3
.param kc=4.03
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0,550))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1E-4
.ends cgdmos_C3M0032120D


.subckt cds_C3M0032120D d3 s1

.param Cjo = 2806p
.param Vj  = 4.4365
.param M   = 0.66

G12 d1 s1 value {
+	if(V(d3,s1)>0,
+		(Cjo/(1+(limit(v(d3,s1),0,514)/Vj)**M))*ddt(v(d3,s1));
+		,
+		0
+			)
+			}

R_CDS d1 d3 1E-4

.ends cds_C3M0032120D
****************************************
.model bodydiode_C3M0032120D d(is=100n bv=1590 EG=5 n=8.4
+	rs=0.0225  trs1=0.00001  trs2=-0.00001 Tnom=25
+	tt=16n ibv=500u Xti=0.13 level=1)







