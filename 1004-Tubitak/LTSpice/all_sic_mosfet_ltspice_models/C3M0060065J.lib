*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C3M0060065J Spice Library
****	Version Beta Date: 11-4-2019
****	Version 1 Date: 03-05-2020
****
***********************************************************
****	Revision record
****	Revision Beta	Preliminary Release
****	Revision 1	Initial Release Rev B 02-2020
****
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S1 = Kelvin Source
****S2,S3,S4,S5,S6 = Power Source
***********************************************************

.subckt C3M0060065J d g s1 s2 s3 s4 s5 s6 Tj Tc
.param p11 = -8
.param p12 = 19
.param Rgint = 3

R1022	Tjc	0	1E6
E1022	Tjc	0 	value {limit(v(Tj),-55,260)}

R100	gk	s	1E6
E100	gk	s 	value {limit(v(g1,s),-8,19)}

e33		NET3	0	Value {-69.44n*V(Tjc)**3+25.85u*V(Tjc)**2-5.629m*V(Tjc)+2.4244}
R_cc		NET3	0 	1E6

xgmos	d3 d1 gk s Tjc NET3 gmos_C3M0060065J

Rg1		g		ga		20.16m
RS1		s1		s1a		6.232m
RS2		s2		s2a		0.4616m
RS3		s3		s3a		0.4616m
RS4		s4		s4a		0.4616m
RS5		s5		s5a		0.4616m
RS6		s6		s6a		0.4616m
Rd		d		da		0.01166m

Lg1		ga		g2		6.027n
Ls1		s1a		s		4.972n
Ls2		s2a		s_c		2.5n
Ls3		s3a		s_C		2.5n
Ls4		s4a		s_C		2.5n
Ls5		s5a		s_C		2.5n
Ls6		s6a		s_C		2.5n
Ld 		da		d3      	1.495n

*RPg   ga		g2		595
*RP1   s1a		s		300
*RP2   s2a		s_c		300
*RP3   s3a		s_c		300
*RP4   s4a		s_c		300
*RP5   s5a		s_c		300
*RP6   s6a		s_c		300
RPd   da		d3		10

R_g 		g1	g2	{Rgint}
R_S		S_C	S_d	1.848m
L_s		S_d	S	1.2615n
R_PLS		S_d	s	300


vdrain_s	d3	d1	0
Gheat		0	Tj	value {abs((V(d1,s)*I(Vdrain_s)))+abs((V(g1,g2)*V(g1,g2)/Rgint))}

xCGD		d3	g1	cgdmos_C3M0060065J
CGS		g1	s	1011p

e15		dd1	d1	value {
+				if (V(gk,s)>V(NET3),
+					Limit(((6.637m*V(Tjc)**2+0.3072*V(Tjc)-271.1)*(V(gk,s)**2)+
+					(-0.1647*V(Tjc)**2-7.649*V(Tj)+6775.9)*v(gk,s)+
+					(0.7729*V(Tjc)**2+40.2*V(Tjc)-30412))/1000,-3.2,15)
+					,
+					0
+					)
+					}

D1		s	dd1	bodydiode_C3M0060065J
xCDS		dk	s	cds_C3M0060065J
*CDS1		dk	s	1p
R_DS1		d1	s	0.5G

D_cd1		dk	d3	ideal_diodev
D_cd2		d3 	dk	ideal_diodew
.model ideal_diodev d(vfwd=0.0 ron=0.5)
.model ideal_diodew d(vfwd=0.0 ron=0)


R0 N1 Tj 54.57m
R1 N2 N1 117.2m
R2 N3 N2 571.8m
R3 Tc N3 361.9m
C0 Tj 0 221.7u
C1 N1 0 958.9u
C2 N2 0 2.419m
C3 N3 0 35.18m

.ends C3M0060065J

*****************************************

.subckt gmos_C3M0060065J d3 d1 gk s Tjc NET3

e1		NET1	0	Value {
+					if (V(Tjc)>=24,
+					Limit(((43.485u*V(Tjc)**2-10.568m*V(Tjc)-0.90899)*(V(gk,s)**3)+
+					(-1.4903m*V(Tjc)**2+0.36615*V(Tjc)+44.591)*(V(gk,s)**2)+
+					(16.27m*V(Tjc)**2-4.0121*V(Tjc)-631.22)*v(gk,s)+
+					(-64.391m*V(Tjc)**2+17.169*V(Tjc)+3315)),100,5200)/1000
+					,
+					Limit(((-748u*V(Tjc)**2-22.44m*V(Tjc)-0.1175)*(V(gk,s)**3)+
+					(27.562m*V(Tjc)**2+0.80194*V(Tjc)+15.538)*(V(gk,s)**2)+
+					(-0.28841*V(Tjc)**2-8.5823*V(Tjc)-326.54)*v(gk,s)+
+					(0.82889*V(Tjc)**2+30.569*V(Tjc)+2421.7)),100,5200)/1000
+					)
+					}
R_a		NET1	0 	1E6



e9	P9	0	value {((108.1u*V(Tjc)**2+32.73m*V(Tjc)+4.246)*(V(gk,s)**2)+
+					(-5.067m*V(Tjc)**2-0.194*V(Tjc)-152.3)*v(gk,s)+
+					(56.47m*V(Tjc)**2-5.765*V(Tjc)+1457.4))/1000}
R_s	P9	0 	1E6

e2		NET2	0	Value {15.35m*V(gk,s)+371.85m}
R_B		NET2	0 	1E6

*e4		NET4	0	Value {0.11}
e4		NET4	0	Value {Limit(((-10.26u*V(Tjc)**2+1.631m*V(Tjc)+1.79)*(V(gk,s)**2)+
+					(414.3u*V(Tjc)**2-49.8m*V(Tjc)-48.57)*v(gk,s)+
+					(-3.87m*V(Tjc)**2+0.3809*V(Tjc)+325.8)),0,2000)/1000}
R_d		NET4	0 	1E6

e5		NET5	0	value {
+					if (V(Tjc)>=24,
+				Limit(((5.8487u*V(Tjc)+1.6405m)*V(gk,s)**6+
+				(-324.8u*V(Tjc)-0.1386)*V(gk,s)**5+
+				(5.5213m*V(Tjc)+4.6919)*V(gk,s)**4+
+				(-10.827m*V(Tjc)-79.44)*V(gk,s)**3+
+				(-0.4436*V(Tjc)+681.42)*V(gk,s)**2+
+				(2.0187*V(Tjc)-2672.8)*v(gk,s)+
+				(4.85*V(Tjc)+4508))/(-1.6667*v(Tjc)+10.34167k),0.001,1)
+					,
+				Limit(((-1.8772u*V(Tjc)**2-33.98u*V(Tjc)+3.8094m)*V(gk,s)**6+
+				(140.45u*V(Tjc)**2+2.7015m*V(Tjc)-0.30203)*V(gk,s)**5+
+				(-4.1972m*V(Tjc)**2-86.295m*V(Tjc)+9.6105)*V(gk,s)**4+
+				(63.509m*V(Tjc)**2+1.4094*V(Tjc)-154.64)*V(gk,s)**3+
+				(-0.50553*V(Tjc)**2-12.262*V(Tjc)+1292.8)*V(gk,s)**2+
+				(1.9615*V(Tjc)**2+52.37*V(Tjc)-5155.6)*v(gk,s)+
+				(-2.8667*V(Tjc)**2-80.326*V(Tjc)+8429.014))
+				/(0.160256*v(Tjc)**2+8.55769*v(Tjc)+10585.9),0.001,1)
+					)
+					}
R_e		NET5	0 	1E6


e10		NET10	0	Value {
+					if (V(Tjc)>=24,
+					Limit(((144.6u*V(Tjc)**2-34.35m*V(Tjc)+1.556)*(V(gk,s)**3)+
+					(-5.441m*V(Tjc)**2+1.319*V(Tjc)-62.23)*(V(gk,s)**2)+
+					(64.19m*V(Tjc)**2-15.85*V(Tjc)+762.4)*v(gk,s)+
+					(-0.2457*V(Tjc)**2+60.44*V(Tjc)-2688)),0,500)/1000
+					,
+					Limit(((-2.298m*V(Tjc)**2-70.99m*V(Tjc)+3.998)*(V(gk,s)**3)+
+					(88.74m*V(Tjc)**2+2.732*V(Tjc)-156.4)*(V(gk,s)**2)+
+					(-1.062*V(Tjc)**2-32.73*V(Tjc)+1888)*v(gk,s)+
+					(3.888*V(Tjc)**2+122.4*V(Tjc)-6822)),0,500)/1000
+					)
+					}
R_K		NET10	0	1E6

e_p8	P8	0	Value {
+					if (V(Tjc)>=24,
+					Limit((107.33n*V(Tjc)**2+78.533u*V(Tjc)+22.97m),0.0005,0.08)
+					,
+					Limit((9.2949u*V(Tjc)**2+216.35u*V(Tjc)+13.782m),0.0005,0.08)
+					)
+					}
R_R	P8	0 	1E6
*
R_T	NET3	0 	1E6
R_V	gk	s 	1E6

********************************
G1 d1 s value {
+	if(V(s,d3)<=0,
+		0
+		,
+		if (V(gk,s)<V(NET3),
+		0
+		,
+		-((((v(NET5)+v(NET4))*(v(gk,s)-V(NET3))))*(1+v(P9)*v(s,d3))*(((log(1+exp(v(gk,s)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3)-(V(NET2)*v(s,d3)*((1+exp(-v(NET10)*v(s,d3)))**v(NET1))))))**2)))
+		)
+			)
+			}
G2 d1 s value {
+	if(V(d3,s)<=0,
+		0
+		,
+		if (V(gk,s)<V(NET3),
+		0
+		,
+		(((v(NET5))*(v(gk,s)-V(NET3))))*(1+v(P8)*v(d3,s))*(((log(1+exp(v(gk,s)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3)-(V(NET2)*v(d3,s)*((1+exp(-v(NET10)*v(d3,s)))**v(NET1))))))**2))
+		)
+			)
+			}

.ends gmos_C3M0060065J
******************************************

.subckt cgdmos_C3M0060065J d3 g1
.param k1=620p
.param k2=0.44
.param ka=50
.param kb=0.4
.param kc=4
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0,280))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1E-4
.ends cgdmos_C3M0060065J

.subckt cds_C3M0060065J dk s

.param a =	969.175
.param b =	2.837495
.param c =	30.66362
.param d =	0.70706

G12 d1 s value {
+	if(V(dk,s)>=0,
+		(((a*b+c*limit(v(dk,s),0,340)**d)/(b+limit(v(dk,s),0,340)**d))*1p)*ddt(v(dk,s))
+		,
+		0
*+		-(a*1p)*ddt(v(s,dk))
+			)
+			}

R_CDS d1 dk 1E-4

.ends cds_C3M0060065J

****************************************
.model bodydiode_C3M0060065J d(is=850n bv=929 EG=5 n=11.05
+	rs=0.086  trs1=-1.4m  trs2=2.2u Tnom=25
+	tt=1n ibv=500u Xti=0.1 level=1)






