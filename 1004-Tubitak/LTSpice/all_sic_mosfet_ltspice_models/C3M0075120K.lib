*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C3M0075120K Spice Library
****	Version 2.0 Date: 11-27-2017
****	Version 3.0 Date: 01-22-2018
****	Version 4.0 Date: 06-19-2018
****	Version 5.0 Date: 09-20-2019
***********************************************************
****	Revision record
****	Version 1	Initial Release
****	Version 2	Include Tc at gmos subcircuit
****	Version 3	This spice model is compatible to both Ltspice and Orcad Pspsice
****	Version 4	Update the thermal RC model
****	Version 5	Update datasheet version D 07-2019 and excluded reactive power loss
***********************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S1 = Kelvin Source
****S2 = Power Source
***********************************************************

.subckt C3M0075120K1 d g s1 s2 Tj Tc
.param Rgint = 9
xgmos	d3 d1 g1 s Tj Tc gmos_C3M0075120K1

RS1		s1	sb	24.88m
Ls1 		sb	sc	7.574n
*R_Ls1 		sb	s       20

RS2		s2	sa	3.322m
Ls2 		sa	sc	3.435n
*R_Ls2 		sa	s      	20
Lss		sc	s	1.5n
R_g 		g1	g2	{Rgint}

RG		g	ga	37.85m
Rg_eq		gb	ga	35
Lg 		gb	g2	11.364n
*R_Lg		gb	g2	20

Rd		d	da	87.42u
Ld 		da	d3      4.366n
R_Ld		da	d3	5

vdrain_s	d3	d1	0

Gheat		0	Tj	value {abs((V(d1,s)*I(Vdrain_s)))+abs((V(g1,g2)*V(g1,g2)/Rgint))}
xCGD		d3	g1	cgdmos_C3M0075120K1
CGS		g1	s	1388p
xCDS		dk	s	cds_C3M0075120K1
D1		s	d1	bodydiode_C3M0075120K1
R_ds		d3	dk	8

R0 N1 Tj 24.985m
R1 N2 N1 59.17m
R2 N3 N2 498.45m
R3 Tc N3 468.96m

C0 Tj 0 396.233u
C1 N1 0 1.22m
C2 N2 0 3.926m
C3 N3 0 355.24m

.ends C3M0075120K1

*****************************************

.subckt gmos_C3M0075120K1 d3 d1 g1 s Tj Tc

e1		NET1	0	Value {Limit(((997.8n*V(Tj)**3-167.2u*V(Tj)**2+2.679m*V(Tj)-97.69m)*V(gk,s)**4+
+				(-48.54u*V(Tj)**3+7.754m*V(Tj)**2-69.85m*V(Tj)+2.697)*V(gk,s)**3+
+				(839.1u*V(Tj)**3-0.1254*V(Tj)**2-0.1785*V(Tj)+1.003)*V(gk,s)**2+
+				(-6.049m*V(Tj)**3+0.8158*V(Tj)**2+15.49*V(Tj)-400.7)*v(gk,s)+
+				(15.16m*V(Tj)**3-1.738*V(Tj)**2-88.88*V(Tj)+3393))/1000,0.01,15)
+					}
R_a		NET1	0 	1E6

e2		NET2	0	Value {15.35m*V(gk,s)+371.85m}
R_B		NET2	0 	1E6

*e3		NET3	0	Value {3.3}
e3		NET3	0	Value {8u*(V(Tj)**2)-4.7m*V(Tj)+2.8224}
R_C		NET3	0 	1E6

*e4		NET4	0	value {0.007}
e4		NET4	0	Value {92.345n*V(Tj)**2-35.295u*V(Tj)+4.792m}
R_d		NET4	0 	1E6

*e5		NET5	0	value {0.035}

e5		NET5   0	Value {
+					if (V(gk,s)>11 ,
*+				(-0.9267*V(gk,s)**3+49.313*V(gk,s)**2-877.727*V(gk,s)+5351.268)/10000
+				((87.641n*V(Tj)**3+46.001u*V(Tj)**2-15.03m*V(Tj)-0.13539)*V(gk,s)**3+
+				(-4.7725u*V(Tj)**3-2.0118m*V(Tj)**2+0.69225*V(Tj)+13.826)*V(gk,s)**2+
+				(86.016u*V(Tj)**3+27.876m*V(Tj)**2-10.322*V(Tj)-366.33)*v(gk,s)+
+				(-496.9u*V(Tj)**3-0.12272*V(Tj)**2+49.167*V(Tj)+3084.3))/10000
+					,
+					if (V(gk,s)<=11 & V(gk,s)>9,
*+				(15*V(gk,s)**2-245*V(gk,s)+1470)/10000
+				((8.3091u*V(Tj)**3+1.2517m*V(Tj)**2-0.30635*V(Tj)-4.25318)*(V(gk,s)**2)+
+				(-166.98u*V(Tj)**3-21.874m*V(Tj)**2+4.7236*V(Tj)+53.187)*v(gk,s)+
+				(821.34u*V(Tj)**3+90.986m*V(Tj)**2-15.564*V(Tj)+475.4))/10000
+					,
*+				(13.958*V(gk,s)**2-158.333*V(gk,s)+774.375)/10000
+				((-8.337u*V(Tj)**3+1.507m*V(Tj)**2-94.69m*V(Tj)+2.806)*(V(gk,s)**2)+
+				(92.64u*V(Tj)**3-16.24m*V(Tj)**2+0.5932*V(Tj)-61.12)*v(gk,s)+
+				(-166.9u*V(Tj)**3+19.64m*V(Tj)**2+4.464*V(Tj)+932.7))/10000
+			)
+			)
+			}
R_e		NET5	0 	1E6

*e10		NET10   0	Value {0.048}
e10		NET10   0	Value {Limit(((-94.87u*V(Tj)**2+25.49m*V(Tj)-0.8726)*(V(gk,s)**3)+
+					(3.038m*V(Tj)**2-0.8788*V(Tj)+35.82)*(V(gk,s)**2)+
+					(-29.94m*V(Tj)**2+9.729*V(Tj)-501.7)*v(gk,s)+
+					(85.19m*V(Tj)**2-34.18*V(Tj)+2452))/1000,0.001,3.7)
+				      }
R_K		NET10	0	1E6

.param p10  = 0.0011
.param p11 = -8
.param p12 = 19
.param p13 = 15

*e_p8	P8	0	Value {0.0122}
e_p8	P8	0	Value {Limit(((95.93n*V(Tj)**3-17.89u*V(Tj)**2+8.478u*V(Tj)+35.59m)*(V(gk,s)**3)+
+				(-4.135u*V(Tj)**3+831u*V(Tj)**2-8.584m*V(Tj)-2.647)*(V(gk,s)**2)+
+				(54.29u*V(Tj)**3-11.48m*V(Tj)**2+0.1753*V(Tj)+51.4)*v(gk,s)+
+				(-216u*V(Tj)**3+46.84m*V(Tj)**2-0.7812*V(Tj)-210.8))/1000,0.001,0.2)
+					}
R_R	P8	0 	1E6

R100 gk s	1E6
E100 gk s 	value {limit(V(g1,s),p11,p12)}


********************************
G1 d1 s value {
+	if(V(s,d3)<0,
+		0
+		,
+		if (V(gk,s)<V(NET3) ,
+		-((0.035)*(v(gk,s)-V(NET3)))*(-(1+p10*v(s,d3))*0.008)*(((log(1+exp(v(gk,s)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3)-(0.854*v(s,d3)))))**2))
+		,
+		-((v(NET5)+v(NET4))*(v(gk,s)-V(NET3)))*(1+v(P8)*v(s,d3))*(((log(1+exp(v(gk,s)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3)-(V(NET2)*v(s,d3)*((1+exp(-v(NET10)*v(s,d3)))**v(NET1))))))**2))
+		)
+			)
+			}
G2 d1 s value {
+	if(V(d3,s)<0,
+		0
+		,
+		if (V(gk,s)<V(NET3) ,
+		((0.035)*(v(gk,s)-V(NET3)))*(-(1+p10*v(d3,s))*0.008)*(((log(1+exp(v(gk,s)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3)-(0.854*v(d3,s)))))**2))
+		,
+		((v(NET5)*(v(gk,s)-V(NET3))))*(1+v(P8)*v(d3,s))*(((log(1+exp(v(gk,s)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3)-(V(NET2)*v(d3,s)*((1+exp(-v(NET10)*v(d3,s)))**v(NET1))))))**2))
+		)
+			)
+			}


.ends gmos_C3M0075120K1

****************************************

.subckt cgdmos_C3M0075120K1 d3 g1
.param k1=555p
.param k2=0.565
.param ka=90
.param kb=0.3
.param kc=6
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0.1,600))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1e-4
.ends cgdmos_C3M0075120K1

.subckt cds_C3M0075120K1 dk s

.param Cjo = 1108p
.param Vj  = 4.5
.param M   = 0.63

G12 d1 s value {
*+	if(V(dk,s)>0,
+		(Cjo/(1+(limit(v(dk,s),0.1,460)/Vj)**M))*ddt(v(dk,s));
*+		,
*+		0
*+			)
+			}

R_CDS d1 dk 1E-4

.ends cds_C3M0075120K1


****************************************
.model bodydiode_C3M0075120K1 d(is=100n bv=1590 EG=5.4 n=9.45
+	rs=0.048 trs1=-650u  trs2=-1.2u Tnom=25
+	tt=3.0n ibv=500u Xti=10 level=1)







