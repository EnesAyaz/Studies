*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C3M0045065D Spice Library
****	Version 1.0 Date: 01-21-2021
****
***********************************************************
****	Revision record
****	Version 1	Initial Release datasheet Rev 1 1-2021
****
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C3M0045065D d g s Tj Tc
.param p11 = -8
.param p12 = 19
.param Rgint = 3

R1022	Tjc	0	1E6
E1022	Tjc	0 	value {limit(v(Tj),-55,260)}

R100	gk	s1	1E6
E100	gk	s1 	value {limit(v(g1,s1),-8,19)}

e33		NET3	0	Value {-127.23n*V(Tjc)**3+42u*V(Tjc)**2-6.975m*V(Tjc)+2.7622}
R_cc		NET3	0 	1E6

xgmos	d3 d1 gk s1 Tjc NET3 gmos_C3M0045065D

RS		s	sa	2.333m
Ls 		sa	s1	3.548n
*R_Ls		sa	s1	5

R_g 		g1	g2	{Rgint}
RG		g	ga	12.272m
Lg 		ga	g2	6.832n

Rd		d	da	64.94u
Ld 		da	d3 	3.848n
R_Ld		da	d3	5.00

Ed		Id	0	value {I(Vdrain_s)}
Rdd		Id	0	1E6

vdrain_s	d3	d1	0
Gheat		0	Tj	value {abs((V(d1,s1)*v(Id)))+abs((V(g1,g2)**2/Rgint))}
xCGD		d3	g1	cgdmos_C3M0045065D
CGS		g1	s1	1613p

e15		dd1	d1	value {
+				if (V(gk,s1)>V(NET3),
+					((-6.88m*V(Tjc)**2+0.214*V(Tjc)+322.45)*(V(gk,s1)**2)+
+					(82.06m*V(Tjc)**2+1.9694*V(Tjc)-2292.5)*v(gk,s1)+
+					(-0.1328*V(Tjc)**2+1.7262*V(Tjc)+681.56))/1000
+					,
+					((5.963u*V(Tjc)**2+0.1345*V(Tjc)-20.859)*(V(gk,s1)**2)+
+					(2.182m*V(Tjc)**2+0.7635*V(Tjc)-490.45)*v(gk,s1)+
+					(8.825m*V(Tjc)**2+0.9016*V(Tjc)-1628.1))/1000
+					)
+					}

D1		s1	dd1	bodydiode_C3M0045065D
xCds		d3	s1	cdsmos_C3M0045065D
Cds		d3	s1	1p
R_DS1		d1	s1	0.3G


R0 N1 Tj 63.78m
R1 N2 N1 0.1553
R2 N3 N2 99.42m
R3 Tc N3 0.1417

C0 Tj 0 1.31m
C1 N1 0 6.698m
C2 N2 0 19.41m
C3 N3 0 73.01m

.ends C3M0045065D

*****************************************

.subckt gmos_C3M0045065D d3 d1 gk s1 Tjc NET3

e2		NET2	0	Value {((-1.264u*V(Tjc)**2+80.88u*V(Tjc)-92.63m)*V(gk,s1)**4+
+				(50.86u*V(Tjc)**2-6.191m*V(Tjc)+4.652)*v(gk,s1)**3+
+				(-439.1u*V(Tjc)**2+0.1205*V(Tjc)-77.68)*v(gk,s1)**2+
+				(-2.504m*V(Tjc)**2-0.799*V(Tjc)+528.9)*v(gk,s1)+
+				(22.6m*V(Tjc)**2+2.805*V(Tjc)-751.3))/1000
+			}
R_B		NET2	0 	1E6

*e6		NET6	0	value {0.0}
*e99		P99	0	value {0.21}

e4		NET4	0	Value {Limit(((35.206u*V(Tjc)**2-22.24m*V(Tjc)+1.414)*(V(gr)**2)+
+					(-1.913m*V(Tjc)**2+0.7067*V(Tjc)-44.071)*v(gr)+
+					(22.068m*V(Tjc)**2-5.854*V(Tjc)+348.55))/1000,0,1)
+					}
R_d		NET4	0 	1E6

*e19		P19	0	value {0.064}
e9		P9	0	value {Limit(((-811.7u*V(Tjc)**2+0.1417*V(Tjc)+26.166)*(V(gr)**2)+
+					(22.74m*V(Tjc)**2-3.505*V(Tjc)-744.59)*v(gr)+
+					(-0.1618*V(Tjc)**2+20.651*V(Tjc)+5444.8))/1000,0.001,10)
+					}
R9		P9	0	1E6

e5		NET5   0	Value {Limit(((3.919u*V(Tjc)**2-3.064m*V(Tjc)+0.3488)*V(gk,s1)**4+
+				(-149.83u*V(Tjc)**2+0.1479*V(Tjc)-16.388)*V(gk,s1)**3+
+				(1.829m*V(Tjc)**2-2.4543*V(Tjc)+257.695)*V(gk,s1)**2+
+				(-8.804m*V(Tjc)**2+15.647*V(Tjc)-1553.13)*v(gk,s1)+
+				(20.015m*V(Tjc)**2-26.83*V(Tjc)+3988.35))/10000,0.001,13)
+			}
R_e		NET5	0 	1E6


e_P8	P8	0	Value {8.511m-8.065m*exp(-46.27*V(gk,s1)**(-2.834))}
R_R	P8	0 	1E6

R1001 gr 0	1E6
E1001 gr 0	value {limit(V(gk,s1),-8,15)}


********************************
G1 d1 s1 value {
+	if(V(s1,d3)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		0
+		,
+		-((((v(NET5)+v(NET4))*(v(gk,s1)-V(NET3))))*(1+v(P9)*v(s1,d3))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(s1,d3)))))**2)))*(0.9699/(1-21.273m*exp(-28.842m*V(Tjc))))
+		)
+			)
+			}

G2 d1 s1 value {
+	if(V(d3,s1)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		0
+		,
+		(((v(NET5))*(v(gk,s1)-V(NET3))))*(1+v(P8)*v(d3,s1))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(d3,s1)))))**2))*(0.9699/(1-21.273m*exp(-28.842m*V(Tjc))))
+		)
+			)
+			}

.ends gmos_C3M0045065D

****************************************
.model bodydiode_C3M0045065D d(is=20n bv=1929 EG=5.98 n=9
+	rs=0.052  trs1=-0.0016  trs2=9u Tnom=25
+	tt=0.5n ibv=500u Xti=0.01 level=1)
****************************************

.subckt cgdmos_C3M0045065D d3 g1
.param k1=715p
.param k2=0.44
.param ka=99
.param kb=0.35
.param kc=3.6
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0.1,250))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**(-k2)
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1E-4
.ends cgdmos_C3M0045065D


.subckt cdsmos_C3M0045065D d3 s1
.param Cjo = 1222p
.param Vj  = 3.3
.param M   = 0.52

G12 d1 s1 value {
*+	if(V(dk,s1)>=0,
+		(Cjo/(1+(limit(v(d3,s1),0.1,400)/Vj)**M))*ddt(v(d3,s1));
*+		,
*+		0
*+		-(a*1p)*ddt(v(s,dk))
*+			)
+			}

R_CDS d1 d3 1E-4
.end cdsmos_C3M0045065D









