*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C3M0065100K Spice Library
****	Version 1.0 Date: 11-28-2016
****	Version 2.0 Date: 03-01-2017
****	Version 3.0 Date: 11-27-2017
****	Version 4.0 Date: 01-22-2018
****	Version 5.0 Date: 06-19-2018
***********************************************************
****	Revision record
****	Version Beta	Initial Release
****	Version 1	Included model number at subcircuit to avoid library overlapping when two Modfet models called
****	Version 2	improved accuracy of package parasitic and switching loss
****	Version 3	Include Tc at gmos subcircuit
****	Version 4	This spice model is compatible to both Ltspice and Orcad Pspsice
****	Version 5	Update the thermal RC model
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S1 = Kelvin Source
****S2 = Power Source
***********************************************************

.subckt C3M0065100K d g s1 s2 Tj Tc

xgmos	d3 d1 g1 s Tj Tc gmos_C3M0065100K
RS1		s1	sb	28.15m
Ls1 		sb	s	6.6835n
R_Ls1 		sb	s       200

RS2		s2	sa	3.56m
Ls2 		sa	s	2.99305n
R_Ls2 		sa	s       200

R_g 		g1	g2	4.7
RG		g	ga	36.09m
Lg 		ga	g3	8.53472n
R_Lg		ga	g3	20

Rd		d	da	0.087m
Ld 		da	d3      4.36n
R_Ld		da	d3	1

G1		d2	d1      Value {V(d1,s)}
R_A		d2	d1	1E6
vgate_s		g3	g2	0
vdrain_s	d3	d2	0
Gheat		0	Tj	value {abs((V(d2,s)*I(Vdrain_s)))+abs((V(g1,g2)*I(Vgate_s)))}

*Eheat		T	0	value {(abs(V(d,s2)*I(Vdrain_s)))}
*Eheat		T	0	value {(abs((V(d,s2)*I(Vdrain_s)))+abs((V(g1,g2)*I(Vgate_s))))}
*R_Heat		T	0	1E6
*GTc			0	Tc1	value {V(Tc)}
*R_Tc		0	Tc	1e6


xCGD		d2	g1	cgdmos_C3M0065100K
CGS		g1	s	656p
D1		s	d2	bodydiode_C3M0065100K
xCds		d2	s	cdsmos_C3M0065100K

R0 N1 Tj 407m
R1 N2 N1 243m
R2 N3 N2 210m
R3 Tc N3 240m
C0 Tj 0 2.95m
C1 N1 0 8.42m
C2 N2 0 61.5m
C3 N3 0 500m

.ends C3M0065100K

*****************************************

.subckt gmos_C3M0065100K d3 d1 g1 s Tj Tc

e3		NET3	0	Value {0.00001*(V(Tj)**2)-0.00735*V(Tj)+2.5308}
R_C		NET3	0 	1E6

e2		NET2	0	Value {3.571m*(V(gk,s)**2)-29.57m*V(gk,s)+0.5206}
R_B		NET2	0 	1E6

e4		NET4	0	value  {
+				if(V(Tj)>50,
+					0
+					,
+					0
+					)
+					}
R_d		NET4	0 	1E6

e5		NET5	0	value  {
+				if(V(gk,s)>13,
+					-491.22n*(V(Tj)**2)-77.24u*v(Tj)+64.23m
+					,
+					0
+					)
+					}
R_e		NET5	0 	1E6

e6		NET6	0	value  {
+				if(V(gk,s)>11 & V(gk,s)<=13,
+					-695.12n*(V(Tj)**2)-58.35u*v(Tj)+88.89m
+					,
+					0
+					)
+					}
R_f		NET6	0 	1E6

e7		NET7	0	value {
+				if(V(gk,s)>9 & V(gk,s)<=11,
+					-1.19u*(V(Tj)**2)+64.3u*v(Tj)+119.14m
+					,
+					0
+					)
+					}
R_g		NET7	0 	1E6

e8		NET8	0	value  {
+				if(V(gk,s)>7 & V(gk,s)<=9,
+					-821.95n*(V(Tj)**2)+287.84u*v(Tj)+133.32m
+					,
+					0
+					)
+					}
R_h		NET8	0 	1E6

e9		NET9	0	value  {
+				if(V(gk,s)>1 & V(gk,s)<=7,
+					914.63n*(V(Tj)**2)+639.94u*v(Tj)+128.43m
+					,
+					0
+					)
+					}
R_j		NET9	0 	1E6

.param p8  = 0.0011
.param p11 = -8
.param p12 = 19

R100 gk s	1E6
E100 gk s 	value {limit(V(g1,s),p11,p12)}


********************************
G1 d1 s value {
+	if(V(s,d3)<0,
+		0
+		,
+		if (V(gk,s)<v(NET3,0) ,
+		-((0.035)*(v(gk,s)-V(NET3,0)))*(-(1+p8*v(s,d3))*0.008)*(((log(1+exp(v(gk,s)-V(NET3,0))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3,0)-(0.854*v(s,d3)))))**2))
+		,
+		-(((v(NET5,0)+v(NET6,0)+v(NET7,0)+v(NET8,0)+v(NET9,0))*(v(gk,s)-V(NET3,0)))+v(NET4,0))*(1+p8*v(s,d3))*
+		(((log(1+exp(v(gk,s)-V(NET3,0))))**2)-((log(1+exp(v(gk,s)-V(NET3,0)-(V(NET2,0)*v(s,d3)))))**2))
+		)
+			)
+			}
G2 d1 s value {
+	if(V(d3,s)<0,
+		0
+		,
+		if (V(gk,s)<v(NET3,0) ,
+		((0.035)*(v(gk,s)-V(NET3,0)))*(-(1+p8*v(d3,s))*0.008)*(((log(1+exp(v(gk,s)-V(NET3,0))))**2)-
+		((log(1+exp(v(gk,s)-V(NET3,0)-(0.854*v(d3,s)))))**2))
+		,
+		(((v(NET5,0)+v(NET6,0)+v(NET7,0)+v(NET8,0)+v(NET9,0))*(v(gk,s)-V(NET3,0)))+v(NET4,0))*(1+p8*v(d3,s))*
+		(((log(1+exp(v(gk,s)-V(NET3,0))))**2)-((log(1+exp(v(gk,s)-V(NET3,0)-(V(NET2,0)*v(d3,s)))))**2))
+		)
+			)
+			}

.ends gmos_C3M0065100K
****************************************
.model bodydiode_C3M0065100K d(is=145.99n bv=1160 EG=6.72827 n=10.4
+	rs=0.043  trs1=0.000281895  trs2=-8.41479u Tnom=25
+	tt=2.5n ibv=500u Xti=0.13 level=1)
****************************************

.subckt cgdmos_C3M0065100K d2 g1
.param k1=135p
.param k2=0.4
.param ka=9
.param kb=0.045
.param kc=5.2277
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d2,g1),0,640))*(1+ka*(1+TANH(kb*V(d2,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d2))
+			}
R_CGD d1 d2 1e-4
.ends cgdmos_C3M0065100K

.subckt cdsmos_C3M0065100K d2 s
.param Cjo = 999.24p
.param Vj  = 7.7708436
.param M   = 0.6600636575

G12 d1 s value {
+	if(V(d2,s)>0,
+		(Cjo/(1+((limit(ABS(v(d2,s)),0,580))/Vj))**M)*ddt(v(d2,s))
+		,
+		0
+			)
+			}

R_CDS d1 d2 1e-4
.ends cdsmos_C3M0065100K











