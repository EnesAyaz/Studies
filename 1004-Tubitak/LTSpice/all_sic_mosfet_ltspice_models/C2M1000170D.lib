*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied 
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************
***********************************************************
****	Cree SiC MOSFET C2M1000170D Spice Library 
****	Version 1.0 Date: 11-28-2016
****	Version 2.0 Date: 01-22-2018
****	Version 3.0 Date: 06-19-2018
***********************************************************
***	Revision record
****	Version Beta	Initial Release
****	Version 1	Included model number at subcircuit to avoid library overlapping when two Modfet models called
****	Version 2	This spice model is compatible to both Ltspice and Orcad Pspsice
****	Version 3	Update the thermal RC model
******************************************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C2M1000170D d g s Tj Tc

xgmos	d1 g1 s1 Tj Tc gmos_C2M1000170D 

Ls 		s	s1	9n
R_Ls 		s	s1      10
Rg 		g1	g2	24.8
Lg 		g	g3	11n
R_Lg		g	g3	10
Ld 		d	d3      6n
R_Ld		d	d3	10

G1		d2	d1      Value {(V(d2,d1))/((0.000016381*((V(Tj))**2)+0.0035238095*(V(Tj))+0.6371428571)+1n)}
R_A		d2	d1	1E12
Vgate_s		g3	g2	0
Vdrain_s	d3	d2	0
Gheat		0	Tj	value {abs((V(d,s)*I(Vdrain_s)))+abs((V(g1,g2)*I(Vgate_s)))}
XCGD		d1	g1	cgdmos_C2M1000170D 
CGS		g1	s1	187.34p
CDS		d2	s1	8.74p
D1		s1	d2	bodydiode_C2M1000170D
R0 N1 Tj 9.24m
R1 N2 N1 11.7m
R2 N3 N2 0.802
R3 N4 N3 0.131
R4 N5 N4 34.5m
R5 N6 N5 69.8m
R6 N7 N6 0.107
R7 N8 N7 41.7m
R8 N9 N8 83.4m
R9 N10 N9 70.9m
R10 N11 N10 54.6m
R11 N12 N11 69.1m
R12 N13 N12 87.3m
R13 Tc N13 24.5m
C0 Tj 0 0.743m
C1 N1 0 1.21m
C2 N2 0 0.761m
C3 N3 0 0.362m
C4 N4 0 2.74m
C5 N5 0 9.92m
C6 N6 0 5.28m
C7 N7 0 16.1m
C8 N8 0 29.5m
C9 N9 0 66.1m
C10 N10 0 0.148
C11 N11 0 0.402
C12 N12 0 0.492
C13 N13 0 1.86

.ends C2M1000170D 
*****************************************

.subckt gmos_C2M1000170D d1 g1 s1 Tj Tc

.param Adj = 0.38
e2		NET1	0	Value {(-0.0032026584*(V(Tj))+1.1825937888)}
R_B		NET1	0 	1E12

e3		NET2	0	Value {((0.0000207653*((V(Tj))**2)-0.0235805580*(V(Tj))+8.4686261698)*Adj)}
R_C		NET2	0 	1E12

.param p3  = 8.7
.param p4  = 0.0257											
.param p5  = 1.94
.param p6  = 2.0
.param p7  = 0.5
.param p8  = 0.001
.param p9  = {2*p3*p4}
.param p10 = {p9*p4}
.param p11 = -10
.param p12 = 25

R100 g2 s1	1e9
E100 g2 s1 	value {limit(V(g1,s1),p11,p12)} 

G1 d1 s1 value {
+	if(V(d1,s1)<0,
+		-p10*V(NET1,0)*((log(1+exp((v(g1,s1)-V(NET2,0))/p9)))**p5)
+		*(1+p8*v(s1,d1))
+		,
+		p10*V(NET1,0)*((log(1+exp((v(g1,s1)-V(NET2,0))/p9)))**p5)
+		*(1+p8*v(d1,s1))
+		)
+			}

G2 d1 s1 value {
+	if(V(d1,s1)<0,
+		p10*V(NET1,0)*((log(1+exp((v(g1,s1)-V(NET2,0)-p6*(v(s1,d1))**p7)/p9)))**p5)
+		*(1+p8*v(s1,d1))
+		,
+		-p10*V(NET1,0)*((log(1+exp((v(g1,s1)-V(NET2,0)-p6*(v(d1,s1))**p7)/p9)))**p5)
+		*(1+p8*v(d1,s1))
+		)
+			}

.ends gmos_C2M1000170D
****************************************

.model bodydiode_C2M1000170D d(is=2e-12 cjo=129.454p bv=2500 m=0.6702 EG=3.26 vj=3.82 n=4.3 
+	rs=0.211  trs1=-0.00822443877  trs2=0.00002636149169 Tnom=25
+	tt=15n ibv=500u Xti=5 level=1)
****************************************

.subckt cgdmos_C2M1000170D d2 g
.param k1=61.72p
.param k2=0.525
.param ka=30.096
.param kb=0.3678
.param kc=5.5277
G11 g d1 value {
+		k1*(
+		(1+(limit(v(d1,g),0,650))*(1+ka*(1+TANH(kb*V(d1,g)-kc))/2))**-k2
+		)*ddt(v(g,d1))
+			}
R_CGD d1 d2 1e-3
C_CGD g d1 1p
.ends cgdmos_C2M1000170D
***************************************
                               