*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Cree SiC MOSFET C3M0040120D Spice Library
****	Version 1.0 Date: 12-18-2020
****
***********************************************************
****	Revision record
****	Version 1	Initial Release datasheet Rev 1 10-2020
****
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C3M0040120D d g s Tj Tc
.param p11 = -8
.param p12 = 19
.param Rgint = 3.5

R1022	Tjc	0	1E6
E1022	Tjc	0 	value {limit(v(Tj),-55,260)}

R100	gk	s1	1E6
E100	gk	s1 	value {limit(v(g1,s1),-8,19)}

e33		NET3	0	Value {-100.64n*V(Tjc)**3+34.691u*V(Tjc)**2-6.7557m*V(Tjc)+2.8904}
R_cc		NET3	0 	1E6

xgmos	d3 d1 gk s1 Tjc NET3 gmos_C3M0040120D

RS		s	sa	2.287m
Ls 		sa	s1	4.0489n
*R_Ls		sa	s1	5

R_g 		g1	g2	{Rgint}
RG		g	ga	12.794m
Lg 		ga	g2	7.132n

Rd		d	da	64.94u
Ld 		da	d3 	3.848n
R_Ld		da	d3	500

Ed		Id	0	value {I(Vdrain_s)}
Rdd		Id	0	1E6

vdrain_s	d3	d1	0
Gheat		0	Tj	value {abs((V(d1,s1)*v(Id)))+abs((V(g1,g2)**2/Rgint))}
xCGD		d3	g1	cgdmos_C3M0040120D
CGS		g1	s1	2895p

e15		dd1	d1	value {
+				if (V(gk,s1)>V(NET3),
+					Limit(((1.019m*V(Tjc)**2+0.2116*V(Tjc)-238.19)*(V(gk,s1)**2)+
+					(-25.46m*V(Tjc)**2-5.289*V(Tj)+5954.72)*v(gk,s1)+
+					(0.1528*V(Tjc)**2+31.734*V(Tjc)-25728.3))/1000,-2.5,15)
+					,
+					0
+					)
+					}

D1		s1	dd1	bodydiode_C3M0040120D
xCds		d3	s1	cdsmos_C3M0040120D
R_DS1		d1	s1	0.3G


R0 N1 Tj 63.78m
R1 N2 N1 0.1553
R2 N3 N2 99.42m
R3 Tc N3 0.1417

C0 Tj 0 1.31m
C1 N1 0 6.698m
C2 N2 0 19.41m
C3 N3 0 73.01m

.ends C3M0040120D

*****************************************

.subckt gmos_C3M0040120D d3 d1 gk s1 Tjc NET3

e2		NET2	0	Value {(71.09n*V(Tjc)**2-10.09u*V(Tjc)-1.33m)*V(gk,s1)**3+
+				(-2.583u*V(Tjc)**2+344.7u*V(Tjc)+59.74m)*v(gk,s1)**2+
+				(29.55u*V(Tjc)**2-3.893m*V(Tjc)-0.7684)*v(gk,s1)+
+				(-117.3u*V(Tjc)**2+15.87m*V(Tjc)+3.533)
+			}
R_B		NET2	0 	1E6

*e6		NET6	0	value {0.0}
*e99		P99	0	value {0.21}

e4		NET4	0	Value {Limit(((448.77u*V(Tjc)**2-71.7m*V(Tjc)-1.2427)*(V(gr)**2)+
+					(-9.968m*V(Tjc)**2+1.672*V(Tjc)+8.7759)*v(gr)+
+					(48.554m*V(Tjc)**2-8.941*V(Tjc)+147.98))/1200,0,1)
+					}
R_d		NET4	0 	1E6

e9		P9	0	value {Limit(((-264.8u*V(Tjc)**2-0.124*V(Tjc)+51.265)*(V(gr)**2)+
+					(0*V(Tjc)**2+4.583*V(Tjc)-1354.1)*v(gr)+
+					(62.99m*V(Tjc)**2-41.978*V(Tjc)+8910.1))/1000,0.001,10)
+					}
R9		P9	0	1E6

e5		NET5   0	Value {Limit(((-7.992u*V(Tjc)**2-1.176m*V(Tjc)+0.186)*V(gk,s1)**4+
+				(371.24u*V(Tjc)**2+62.938m*V(Tjc)-6.9605)*V(gk,s1)**3+
+				(-6.124m*V(Tjc)**2-1.085*V(Tjc)+66.642)*V(gk,s1)**2+
+				(41.24m*V(Tjc)**2+6.029*V(Tjc)-2.302)*v(gk,s1)+
+				(-84.028m*V(Tjc)**2-1.3701*V(Tjc)+95.627))/10000,0.001,13)
+			}
R_e		NET5	0 	1E6


e_p8	P8	0	Value {0.0043}
R_R	P8	0 	1E6

R1001 gr 0	1E6
E1001 gr 0	value {limit(V(gk,s1),-8,15)}


********************************
G1 d1 s1 value {
+	if(V(s1,d3)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		0
+		,
+		-((((v(NET5)+v(NET4))*(v(gk,s1)-V(NET3))))*(1+v(P9)*v(s1,d3))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(s1,d3)))))**2)))*(-4.067u*V(Tjc)**2+246.691u*V(Tjc)+1.0664)
+		)
+			)
+			}

G2 d1 s1 value {
+	if(V(d3,s1)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		0
+		,
+		(((v(NET5))*(v(gk,s1)-V(NET3))))*(1+v(P8)*v(d3,s1))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(d3,s1)))))**2))*(-4.067u*V(Tjc)**2+246.691u*V(Tjc)+1.0664)
+		)
+			)
+			}

.ends gmos_C3M0040120D

****************************************
.model bodydiode_C3M0040120D d(is=80n bv=1590 EG=4.72 n=9.1
+	rs=0.038  trs1=-0.00285  trs2=6u Tnom=25
+	tt=10n ibv=500u Xti=0.13 level=1)
****************************************

.subckt cgdmos_C3M0040120D d3 g1
.param k1=1074p
.param k2=0.5
.param ka=85
.param kb=0.4
.param kc=4.0277
G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0.1,450))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**(-k2)
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1E-4
.ends cgdmos_C3M0040120D


.subckt cdsmos_C3M0040120D d3 s1
.param Cjo = 2256p
.param Vj  = 4.3
.param M   = 0.64

G12 d1 s1 value {
*+	if(V(dk,s1)>=0,
+		(Cjo/(1+(limit(v(d3,s1),0.1,490)/Vj)**M))*ddt(v(d3,s1));
*+		,
*+		0
*+		-(a*1p)*ddt(v(s,dk))
*+			)
+			}

R_CDS d1 d3 1E-4
.ends cdsmos_C3M0040120D









