*********************************************************************************
*										*
*	    ,o888888o.    888888888888.   88888888888888 88888888888888		*
*	   8888     `88.  888888    `88.  888888         888888         	*
*	,888888       `8. 888888     `88  888888         888888         	*
*	8888888           888888     ,88  888888         888888         	*
*	8888888           888888.   ,88'  88888888888888 88888888888888 	*
*	8888888           888888888888'   888888         888888         	*
*	8888888           888888`8b       888888         888888         	*
*	`888888       .8' 888888 `8b.     888888         888888         	*
*	   8888     ,88'  888888   `8b.   888888         888888         	*
*	    `8888888P'    888888     `88. 88888888888888 88888888888888		*
*										*
*********************************************************************************
*******************************************************************************
**	DISCLAIMER
*******************************************************************************
**	This model is provided as is, where is, and with no warranty of any kind
**	either expressed or implied, including but not limited to any implied
**	warranties of merchantability and fitness for a particular purpose.
*******************************************************************************

***********************************************************
****	Wolfspeed SiC MOSFET C3M0021120D Spice Library
****	Version 1.0 Date: 10-25-2019
****
***********************************************************
****	Revision record
****	Version 1	Initial Release - Datasheet 08-2019
****
*********************************************************************************
****Parasitics Included
****Tj = Junction Temperature
****Tc = Case Temperature
****D = Drain
****G = Gate
****S = Source
***********************************************************

.subckt C3M0021120D d g s Tj Tc 
.param Rgint = 3.3

xgmos	d3 d1 g1 s1 Tj Tc gmos_C3M0021120D

RS		s	sa	1.855m
Ls 		sa	s1	7.73n
*R_Ls		sa	s1	10

R_g 		g1	g2	{Rgint}
RG		g	ga	13.168m
Lg 		ga	g2	10.7n
*R_Lg		ga	g2	10

Rd		d	da	64.94u
Ld 		da	d3      3.848n
*R_Ld		da	d3	10

vdrain_s	d3	d1	0
Gheat		0	Tj	value {abs((V(d1,s1)*I(Vdrain_s)))+abs((V(g1,g2)**2/Rgint))}
xCGD		d3	g1	cgdmos_C3M0021120D
CGS		g1	s1	4810p
xCds		d3	s1	cds_C3M0021120D
D1		s1	d1	bodydiode_C3M0021120D

R_DS1		d1	s1	0.8G


R0 N1 Tj 44.104m
R1 N2 N1 103.97m
R2 N3 N2 63.779m
R3 Tc N3 112.065m

C0 Tj 0 2.214m
C1 N1 0 12.602m
C2 N2 0 34.161m
C3 N3 0 129.425m

.ends C3M0021120D

*****************************************

.subckt gmos_C3M0021120D d3 d1 g1 s1 Tj Tc 

e1		NET1	0	Value {((925.2u*V(Tj)**2-0.1998*V(Tj)+22.12)*(V(gk,s1)**2)+
+					(-23.03m*V(Tj)**2+4.789*V(Tj)-433.7)*v(gk,s1)+
+					(0.1278*V(Tj)**2-27.43*V(Tj)+3136))/1000
+					}
R_a		NET1	0 	1E6

e2		NET2	0	Value {15.35m*V(gk,s1)+371.85m}
R_B		NET2	0 	1E6

e3		NET3	0	Value {10u*(V(Tj)**2)-5.5m*V(Tj)+2.6226}
R_C		NET3	0 	1E6

e4		NET4	0	Value {limit((18.539m-84.469u*V(Tj))/(1+5.98m*V(Tj)+33.233u*V(Tj)**2),0,0.055)}
R_d		NET4	0 	1E6

e5		NET5   0	Value {
+					if (V(Tj)>=25,
+				Limit(((23.29n*V(Tj)**2+5.529u*V(Tj)+1.531m)*V(gk,s1)**6+
+				(-1.804u*V(Tj)**2+377.556u*V(Tj)-0.2155)*V(gk,s1)**5+
+				(56.889u*V(Tj)**2-36.591m*V(Tj)+9.125)*V(gk,s1)**4+
+				(-934.4u*V(Tj)**2+0.94376*V(Tj)-166.679)*V(gk,s1)**3+
+				(8.419m*V(Tj)**2-10.218*V(Tj)+1363.49)*V(gk,s1)**2+
+				(-39.434m*V(Tj)**2+43.443*V(Tj)-4217.12)*v(gk,s1)+
+				(74.934m*V(Tj)**2-36.886*V(Tj)+4377.94))/10800,0.0001,1)
+				,
+				Limit(((-2.0647u*V(Tj)**2+147.18u*V(Tj)-705.1u)*V(gk,s1)**6+
+				(157.06u*V(Tj)**2-10.2m*V(Tj)-50.373m)*V(gk,s1)**5+
+				(-4.81m*V(Tj)**2+0.2705*V(Tj)+4.49)*V(gk,s1)**4+
+				(74.55m*V(Tj)**2-3.387*V(Tj)-105.6)*V(gk,s1)**3+
+				(-0.5967*V(Tj)**2+19.76*V(Tj)+992.1)*V(gk,s1)**2+
+				(2.177*V(Tj)**2-45.926*V(Tj)-3368)*v(gk,s1)+
+				(-2.302*V(Tj)**2+47.59*V(Tj)+3752))/10000,0.0001,1)
+			)
+			}
R_e		NET5	0 	1E6

e10		NET10   0	Value {Limit(((46.07u*V(Tj)**2-10.44m*V(Tj)+1.42)*(V(gk,s1)**3)+
+					(-1.735m*V(Tj)**2+0.3991*V(Tj)-47.6)*(V(gk,s1)**2)+
+					(22.57m*V(Tj)**2-5.049*V(Tj)+486.1)*v(gk,s1)+
+					(-0.1012*V(Tj)**2+21.11*V(Tj)-1362))/1000,0.0001,1)
+				      }
R_K		NET10	0	1E6

.param p10  = 0.0011
.param p11 = -8
.param p12 = 19
.param p13 = 15


e_p8	P8	0	Value {Limit(((23.5u*V(Tj)**2-2.112m*V(Tj)-0.5059)*(V(gk,s1)**2)+
+					(-674.6u*V(Tj)**2+61.57m*V(Tj)+15.17)*v(gk,s1)+
+					(5.816m*V(Tj)**2-0.7002*V(Tj)-57.07))/
+					(V(dds)),0.00001,1)
+					}
R_R	P8	0 	1E6

R100 gk s1	1E6
E100 gk s1	value {limit(V(g1,s1),p11,p12)}


R103 dds 0	1E6
E103 dds 0	value {
+			if (V(d3,s1)>20 ,
+			1050
*+			Limit(-14.862m*V(d3,s1)**2+26.457*v(d3,s1)+418.126,1000,50000)
+			,
+			1000
+			)
+			}

********************************
G1 d1 s1 value {
+	if(V(s1,d3)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		0
+		,
+		-((v(NET5)+v(NET4))*(v(gk,s1)-V(NET3)))*(1+v(P8)*v(s1,d3))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(s1,d3)*((1+exp(-v(NET10)*v(s1,d3)))**v(NET1))))))**2))
+		)
+			)
+			}

G2 d1 s1 value {
+	if(V(d3,s1)<=0,
+		0
+		,
+		if (V(gk,s1)<v(NET3) ,
+		0
+		,
+		((v(NET5)*(v(gk,s1)-V(NET3))))*(1+v(P8)*v(d3,s1))*(((log(1+exp(v(gk,s1)-V(NET3))))**2)-
+		((log(1+exp(v(gk,s1)-V(NET3)-(V(NET2)*v(d3,s1)*((1+exp(-v(NET10)*v(d3,s1)))**v(NET1))))))**2))
+		)
+			)
+			}

.ends gmos_C3M0021120D

****************************************
.subckt cgdmos_C3M0021120D d3 g1

.param k1=1810p
.param k2=0.475
.param ka=80
.param kb=0.5
.param kc=6

G11 g1 d1 value {
+		k1*(
+		(1+(limit(v(d3,g1),0,400))*(1+ka*(1+TANH(kb*V(d3,g1)-kc))/2))**-k2
+		)*ddt(v(g1,d3))
+			}
R_CGD d1 d3 1E-4
.ends cgdmos_C3M0021120D


.subckt cds_C3M0021120D d3 s1

.param Cjo = 3835p
.param Vj  = 3
.param M   = 0.615

G12 d1 s1 value {
+	if(V(d3,s1)>=0,
+		(Cjo/(1+(limit(v(d3,s1),0,458)/Vj)**M))*ddt(v(d3,s1));
+		,
+		0
+			)
+			}

R_CDS d1 d3 1E-4

.ends cds_C3M0021120D
****************************************
.model bodydiode_C3M0021120D d(is=200n bv=1590 EG=5.4 n=8.4
+	rs=0.013  trs1=-1.75m  trs2=14u Tnom=25
+	tt=25n ibv=500u Xti=10 level=1)








